// Account Module Sync

// ZCRM variables
recordId = 6943258344300003;
record = zoho.crm.getRecordById("Accounts",recordId);
syncMap = Map();

// Map for the address field subfield values
recordAddressList = Map();

// ZCreator variables
ownerName = "althon.devtac";
appLinkName = "creator-and-crm";

// Optional map is required when requesting outside the current app
optionalMap = Map();

// Fields to be synced
syncMap.put("CRM_Id",recordId);
syncMap.put("Account_Name",record.get("record_Name"));
syncMap.put("Website",{"url":record.get("Website")});

// Supplying the address field subfields
recordAddressList.put("address_line_1",record.get("Billing_Street"));
recordAddressList.put("district_city",record.get("Billing_City"));
recordAddressList.put("state_province",record.get("Billing_State"));
recordAddressList.put("postal_Code",record.get("Billing_Code"));
recordAddressList.put("country",record.get("Billing_Country"));

// Adding the list of subfield values in address field
syncMap.put("Address_Information",recordAddressList);

// Check for existing record to avoid duplicate entries
existingRecord = zoho.creator.getRecords(ownerName, appLinkName, "All_Accounts", "CRM_Id == \"" + recordId + "\"", 1, 1, "zcreator_conn");
if(existingRecord.get("data").isNull())
{
	// If there's no existing record in ZCreator then it will create a new record
	// Sync creation request from ZCRM to ZCreator
	sync = zoho.creator.createRecord(ownerName, appLinkName, "Account", syncMap, optionalMap, "zcreator_conn");
}
else
{
	// If there's an existing record in ZCreator then it will update the existing record
	// Sync update request from ZCRM to ZCreator
	existingRecordId = existingRecord.get("data").getJSON("ID");
	sync = zoho.creator.updateRecord(ownerName, appLinkName, "All_Accounts", existingRecordId, syncMap, optionalMap, "zcreator_conn");
}
// Checking request response
info sync;